import random


@value
struct Grid(Stringable, Writable):
    var rows: Int
    var cols: Int
    var data: List[List[Int]]

    fn __getitem__(self, row: Int, col: Int) -> Int:
        return self.data[row][col]

    fn __setitem__(mut self, row: Int, col: Int, value: Int):
        self.data[row][col] = value

    fn __str__(self) -> String:
        var str = String()
        for row in range(self.rows):
            for col in range(self.cols):
                if self[row, col] == 1:
                    str += "X"
                else:
                    str += " "
            if row != self.rows - 1:
                str += "\n"
        return str

    fn write_to[W: Writer](self, mut writer: W):
        writer.write(self.__str__())

    @staticmethod
    fn random(rows: Int, cols: Int) -> Self:
        random.seed()

        var data = List[List[Int]]()

        for _ in range(rows):
            var row_data = List[Int]()
            for _ in range(cols):
                row_data.append(int(random.random_si64(0, 1)))
            data.append(row_data)

        return Self(rows, cols, data)

    fn evolve(self) -> Self:
        var next_generation = List[List[Int]]()

        for row in range(self.rows):
            var row_data = List[Int]()
            var row_above = (row - 1) % self.rows
            var row_below = (row + 1) % self.rows

            for col in range(self.cols):
                var col_left = (col - 1) % self.cols
                var col_right = (col + 1) % self.cols

                # Calculate the number of populated neighbors
                var num_neighbors = (
                    self[row_above, col_left]
                    + self[row_above, col]
                    + self[row_above, col_right]
                    + self[row, col_left]
                    + self[row, col_right]
                    + self[row_below, col_left]
                    + self[row_below, col]
                    + self[row_below, col_right]
                )

                # Determine the state of the current cell for the next generation
                var new_state = 0
                if self[row, col] == 1 and (
                    num_neighbors == 2 or num_neighbors == 3
                ):
                    new_state = 1
                elif self[row, col] == 0 and num_neighbors == 3:
                    new_state = 1
                row_data.append(new_state)

            next_generation.append(row_data)

        return Self(self.rows, self.cols, next_generation)
