from grid import Grid
from python import Python
import time


fn run_display(
    owned grid: Grid,
    window_height: Int = 600,
    window_width: Int = 600,
    background_color: String = "black",
    cell_color: String = "green",
    pause: Float64 = 0.01,
) raises:
    var pygame = Python.import_module("pygame")
    pygame.init()

    var window = pygame.display.set_mode((window_height, window_width))
    pygame.display.set_caption("Conway's Game of Life")

    var cell_height = window_height / grid.rows
    var cell_width = window_width / grid.cols
    var border_size = 1
    var cell_fill_color = pygame.Color(cell_color)
    var background_fill_color = pygame.Color(background_color)

    running = True
    while running:
        # Poll for events
        var event = pygame.event.poll()
        if event.type == pygame.QUIT:
            running = False
        elif event.type == pygame.KEYDOWN:
            # Also quit if the user presses <Escape> or <q>
            if event.key == pygame.K_ESCAPE or event.key == pygame.K_q:
                running = False

        window.fill(background_fill_color)

        # Draw each live cell in the grid
        for row in range(grid.rows):
            for col in range(grid.cols):
                if grid[row, col]:
                    x = col * cell_width + border_size
                    y = row * cell_height + border_size
                    width = cell_width - border_size
                    height = cell_height - border_size
                    pygame.draw.rect(
                        window, cell_fill_color, (x, y, width, height)
                    )

        pygame.display.flip()

        time.sleep(pause)
        grid = grid.evolve()

    pygame.quit()


fn main() raises:
    start = Grid.random(128, 128)
    run_display(start)
